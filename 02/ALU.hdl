// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // x flags
    Mux16(a=x, b=false, sel=zx, out=xpt1);
    Not16(in=xpt1, out=nxpt1);
    Mux16(a=xpt1, b=nxpt1, sel=nx, out=xpt2);

    // y flags
    Mux16(a=y, b=false, sel=zy, out=ypt1);
    Not16(in=ypt1, out=nypt1);
    Mux16(a=ypt1, b=nypt1, sel=ny, out=ypt2);

    // f(0) : &, f(1): +
    And16(a=xpt2, b=ypt2, out=opand);
    Add16(a=xpt2, b=ypt2, out=opadd);
    Mux16(a=opand, b=opadd, sel=f, out=opt1);

    // no flag
    Not16(in=opt1, out=nopt1);
    Mux16(a=opt1, b=nopt1, sel=no, out=out, out=opt2, out[15]=ng);

    // zr flag
    ZeroCheck(in=opt2, out=zr);

}
